comments = r'[/][/].*|[/][*](.|\n)*?[*][/]'
identifier = r'[a-zA-Z]\w*'

program_start = statement* expression EOF
statement = declaration / assignment / if / while / for
declaration = 'var' decl_variable (',' decl_variable)* ';'
decl_variable = identifier
assignment = lhs_variable '=' expression ';'
lhs_variable = identifier
if = 'if' expression block ( 'else' block )?
block = '{' statement* '}'
while = 'while' expression block
for = 'for' for_variable '=' expression ('upto' / 'downto') expression block
for_variable = identifier

expression_start = expression EOF
expression = multiplication (('+' / '-') multiplication)*
multiplication = unary (('*' / '/' / '%') unary)*
unary = ('+' / '-' / '!')* primary
primary = decimal / boolean / parenthesis / rhs_variable
decimal = r'\d+'
boolean = 'true' / 'false'
parenthesis = '(' expression ')'
rhs_variable = identifier
